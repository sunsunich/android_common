apply plugin: 'org.ajoberstar.grgit'

ext.commonAndroid = {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 27

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
}

ext.app_compat_version = "27.1.1"

ext.dependencies = {
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')

    implementation "com.android.support:appcompat-v7:$app_compat_version"

    implementation 'com.squareup.moshi:moshi:1.5.0'
    implementation 'joda-time:joda-time:2.9.3'
    implementation 'commons-io:commons-io:2.5'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}


static def getDate() {
    new Date().format('yyyyMMdd')
}

ext.getFullVersion = { int versionMajor, int versionMinor, int versionPatch ->
    def versionCode = versionMajor * 10000 + versionMinor * 100 + versionPatch

    // Use CircleCI build number if available, for local builds use current date and commit hash
    def versionBranchSuffix = ""
    if (System.getenv("CIRCLE_BRANCH") != null) {
        if (System.getenv("CIRCLE_BRANCH").toLowerCase().equals("master")) {
            versionBranchSuffix = ""
        } else if (System.getenv("CIRCLE_BRANCH").toLowerCase().equals("develop")) {
            versionBranchSuffix = "-D"
        } else {
            versionBranchSuffix = "-F"
        }
    }
    def versionNameSuffix = System.getenv("CIRCLE_BUILD_NUM") != null ? "-" + System.getenv("CIRCLE_BUILD_NUM") : "-${getDate()}#${grgit.head().abbreviatedId}"
    versionNameSuffix += versionBranchSuffix
    def versionName = "${versionMajor}.${versionMinor}.${versionPatch}"
    return new Tuple(versionCode, versionNameSuffix, versionName)
}
